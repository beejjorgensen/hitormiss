10 SN=54272:SB=1024:CB=55296:HS=0:SD=0:R=214
20 POKE 650,128:POKE 53281,0:POKE 53280,0
30 FOR X=SN TO SN+24:POKE X,0:NEXT X
40 POKE SN+5,128: POKE SN+6,128:POKE SN+24,15
50 S$=CHR$(32):L$=S$:FOR X=1 TO 22:L$=L$+S$:NEXT X
60 B$=CHR$(113):FOR X=2 TO 28
70 B$=B$+CHR$(113-10*(X>12 AND X<17)):NEXT X
80 M$=CHR$(18)+"/////////////MISS/////////////"
90 H$=CHR$(18)+"=============HIT!============="
100 Q$=CHR$(5)+CHR$(118):E$=CHR$(144)+S$
110 P$=CHR$(18)+CHR$(156)+"---":N$=CHR$(144)+S$+S$+S$
120 CX=INT(RND(1)*25)+9:IF CX>20 and CX<25 THEN 120
130 RX=11:FL=-1:DR=-1:DC=-1:SC=0
140 HC=CX:HR=RX:NP=22:LP=19:M=0
150 PRINT CHR$(147):POKE SN,198:POKE SN+1,45
160 POKE R,5:PRINT:PRINT CHR$(5);" YOUR":PRINT " SCORE:":PRINT:PRINT SC
170 POKE R,12:PRINT:PRINT " HIGH":PRINT " SCORE:"
180 PRINT:PRINT CHR$(5);HS
190 POKE R,10:PRINT:PRINT TAB(12);"PRESS ANY KEY TO BEGIN.";CHR$(19)
200 K1=INT(RND(1)*3)+152:K2=INT(RND(1)*7)+149:K0=K1
210 PRINT:FOR X=1 TO 19:PRINT TAB(8);CHR$(18);CHR$(156);S$;
220 IF X<9 OR X>11 THEN PRINT CHR$(146);CHR$(KO);B$;
230 PRINT TAB(37);CHR$(18);CHR$(156);S$
240 IF X=11 THEN KO=K2
250 NEXT X
260 POKE R,22:PRINT:PRINT TAB(18);"PADDLE KEYS:"
270 PRINT TAB(13);"B = LEFT    N = RIGHT";CHR$(19)
280 GOSUB 1030
290 GET K$:IF K$="" THEN 290
300 POKE R,10:PRINT:PRINT TAB(12);L$
310 IF RND(0)>0.05 THEN 360
320 CH=42:IF M>300 AND RND(1)>0.5 THEN CH=123
330 XR=INT(RND(1)*16)+3
340 IF XR>9 AND XR<13 THEN 330
350 POKE R,XR-1:PRINT:PRINT TAB(INT(RND(1)*21)+10);CHR$(5);CHR$(CH)
360 CX=CX+DC:RX=RX+DR
370 PE=PEEK(SB+CX+RX*40)
380 IF PE=81 OR PE=91 THEN SC=SC+20:M=M+1
390 IF PE=42 THEN SC=SC+500:SD=33:POKE SN+4,SD
400 GOSUB 2000:IF PE=91 THEN DC=-DC:GOTO 520
410 IF CX>8 AND CX<37 THEN 440
420 DC=-DC:SD=129:POKE SN+4,SD:CX=CX+DC
430 PE=PEEK(SB+CX+RX*40)
440 IF RX>1 AND RX<21 AND PE<>173 THEN 520
450 DR=-DR:SD=129:POKE SN+4,SD
460 IF RX<2 THEN RX=3:IF NOT FL THEN 620
470 IF RX>20 THEN RX=19:IF FL THEN 620
480 IF PE=173 THEN RX=RX+2*DR:GOTO 520
490 IF RX=11 THEN PRINT PE:STOP
500 SC=SC+M:GOSUB 2000
510 IF RND(1)>0.7 THEN GOSUB 1000
520 POKE R,HR-1:PRINT:PRINT TAB(HC);S$
530 POKE R,RX-1:PRINT:PRINT TAB(CX);Q$
540 HC=CX:HR=RX:IF RX=11 THEN 310
550 GET K$:NP=NP+2*((K$="B")-(K$="N")):IF SD THEN POKE SN+4,SD-1:SD=0
560 IF LP=NP THEN 310
570 IF NP<9 THEN NP=9
580 IF NP>34 THEN NP=34
590 POKE R,10:PRINT:PRINT TAB(LP);N$
600 POKE R,10:PRINT:PRINT TAB(NP);P$
610 LP=NP:GOTO 310
620 POKE SN+4,33:FOR DE=1 TO 25
630 POKE R,HR-1:PRINT:PRINT TAB(HC);CHR$(INT(RND(1)*2)+118)
640 POKE SN,38:POKE SN+1,INT(RND(1)*69)+1:NEXT DE
650 POKE SN+4,0
660 POKE R,23:PRINT
670 FOR DE=1 TO 10:PRINT CHR$(13):NEXT DE
680 PRINT CHR$(158);"SORRY, YOU MISSED."
690 PRINT:PRINT "YOUR SCORE WAS";SC;"POINTS."
700 IF SC>HS THEN HS=SC:PRINT:PRINT CHR$(159);"A NEW RECORD!";CHR$(158)
710 PRINT:PRINT "THE HIGH SCORE IS";HS;"POINTS."
720 PRINT:PRINT:PRINT:PRINT CHR$(150);" PLEASE SELECT:":PRINT
730 PRINT " <R>EPLAY"
740 PRINT " <Q>UIT"
750 PRINT:PRINT:PRINT:PRINT
760 GET K$:IF K$="Q" THEN END
770 IF K$<>"R" THEN 760
780 GOTO 120
1000 POKE SN+4,17:FL=NOT FL:IF FL THEN 1030
1010 POKE R,1:PRINT:PRINT CHR$(19);CHR$(28);TAB(48);M$
1020 POKE R,20:PRINT:PRINT CHR$(158);TAB(8);H$:RETURN
1030 POKE R,1:PRINT:PRINT CHR$(19);CHR$(158);TAB(48);H$
1040 POKE R,20:PRINT:PRINT CHR$(28);TAB(8);M$:RETURN
2000 POKE R,8:PRINT:PRINT CHR$(5);SC:RETURN
